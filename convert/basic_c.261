cursor           cmp #on_token                            ; Check for ON | OFF
                 clc                                      
                 beq _local_1260_20                       ; turn cursor on (.c=0)
                 cmp #esc_command_token                   
                 bne _local_1260_1                        ; (might be a function)
                 jsr chkesc                               
                 cmp #off_token                           ; turn cursor off (.c=1)
                 beq _local_1260_20                       
                 +lbra snerr                              

_local_1260_1    pha                                      ; Evaluate cursor position parameters
                 sec                                      
                 jsr _plot                                ; get current cursor position & save it
                 stx srow                                 
                 sty column                               

                 ldx column                               ; get new column, default=current column
                 pla                                      
                 cmp #','                                 
                 beq _local_1260_10                       ; not given, use default
                 jsr getbyt                               
_local_1260_10   stx column                               
                 ldx srow                                 ; get new row, default=current row
                 jsr optbyt                               
; stx srow
                 ldy column                               
                 clc                                      
                 jsr _plot                                ; set new cursor position
                 +lbcs fcerr                              ; error if bad position

                 jsr optzer                               ; Get new cursor type   ???? assumes screen output
                 bcc _local_1260_30                       ; not given, exit
                 lda #esc                                 
                 jsr _bsout                               ; use escape sequence to set
                 txa                                      
                 and #1                                   
                 eor #1                                   ; [910808]
                 clc                                      
                 adc #'E'                                 ; 0=F=flash, 1=E=solid
                 jmp _bsout                               ; set it and exit

_local_1260_20   jsr _cursor                              ; Turn cursor ON or OFF per .c

                 jsr chrget                               ; eat token, get next character
                 beq _local_1260_30                       ; eol- exit
                 jsr chkcom                               ; else, must be comma
                 bra _local_1260_1                        ; it is- go evaluate position

_local_1260_30   rts                                      ; eol

; .page
;************************************************************************
;*   RCURSOR Returns in variable list current cursor position *
;*         *
;*   Syntax: RCURSOR [column [,row] ]    *
;************************************************************************

