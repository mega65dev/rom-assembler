reser            sta index1+1                             
                 sty index1                               
                 ldy #0                                   
                 sty count                                
                 dey                                      
_local_1009_10   iny                                      
_local_1009_20   lda (txtptr),y                           ; assumes common memory
                 bmi _local_1009_70                       ; abrieviation    [900510]
                 sec                                      
                 sbc (index1),y                           ; does letter match? (ind.ok)
                 beq _local_1009_10                       ; yes...continue
                 cmp #$80                                 ; end of word?
                 beq _local_1009_60                       ; yes...c set...done


;  find next word

_local_1009_30   lda (index1),y                           ; ind.ok
                 bmi _local_1009_40                       ; found end of current
                 iny                                      
                 bne _local_1009_30                       
_local_1009_40   iny                                      ; start of next
                 inc count                                ; value of token
                 clc                                      
                 tya                                      
                 adc index1                               
                 sta index1                               
                 bcc _local_1009_50                       
                 inc index1+1                             
_local_1009_50   clc                                      
                 ldy #0                                   
                 lda (index1),y                           ; end of list? ind.ok
                 bne _local_1009_20                       ; no


;  yes...carry clear...fail

_local_1009_60   ora count                                ; .a=$80 if match
                 sta count                                ; token is formed
                 rts                                      


; special case- last character is shifted (necessary for 'diR' compatibility)

_local_1009_70   sec                                      ; allow last chr to be shifted   [900510]
                 sbc (index1),y                           ; does letter match? (ind.ok)
                 beq _local_1009_80                       ; yes- end of word
                 cmp #$80                                 ; end of word?
                 beq _local_1009_60                       ; yes
                 bne _local_1009_30                       ; no- next word

_local_1009_80   lda #$80                                 ; last chr is shifted & so is end of current word
                 bra _local_1009_60                       

;.end
; .page
; .subttl Non-Escape Keyword List

