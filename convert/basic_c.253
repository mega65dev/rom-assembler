movspr_irq                                                
                 lda vic+21                               ; any sprites active?    [910212]
                 +lbeq music_irq                          ; no- skip ahead

                 ldy #7                                   ; check each of 8 sprites
_local_1252_10   lda vic+21                               ; is this sprite is enabled?
                 and sbits,y                              
                 beq _local_1252_40                       ; sprite not enabled

                 ldx sproff,y                             ; get offset to sprite info from a table
                 lda sprite_data,x                        ; is this sprite moving (speed >0 )?
                 beq _local_1252_40                       ; sprite not moving
                 bpl _local_1252_15                       ; sprite moving, no destination
                 bsr movspr_to_irq                        ; sprite moving to a destination [910809]
                 bra _local_1252_40                       

_local_1252_15   sta sprite_data+1,x                      ; set counter
_local_1252_20   tya                                      ; convert sprite# to a VIC register pointer
                 asl                                      
                 tay                                      
                 lda sprite_data+2,x                      ; get angle sign
                 dec                                      ; subtract 1 for cosine
                 inx                                      
                 inx                                      
                 iny                                      
                 jsr sprsub                               ; update y position
                 dex                                      
                 dex                                      
                 dey                                      
                 lda sprite_data+2,x                      
                 jsr sprsub                               ; update x position
                 php                                      
                 tya                                      
                 lsr                                      ; restore index (.Y=sprite pointer)
                 tay                                      
                 plp                                      
                 bcc _local_1252_30                       ; skip if no overflow
                 lda vic+16                               ; get x position msb bits ???vic_save
                 eor sbits,y                              ; invert bit
                 sta vic+16                               ; ???vic_save
_local_1252_30   dec sprite_data+1,x                      
                 bne _local_1252_20                       ; loop until counter done

_local_1252_40   dey                                      ; check next sprite
                 bpl _local_1252_10                       ; loop until done moving all sprites
                 +lbra music_irq                          ; then continue with next IRQ task
; .page
