play_rts                                                  
                 rts                                      

; .page
play_one_character                                          
                 cmp #' '                                 ; spaces are a 'no-op'
                 beq play_rts                             

_local_1103_5    cmp #'A'                                 ; note name a-g?
                 bcc _local_1103_10                       
                 cmp #'H'                                 
                 +lbcc play_note                          ; yes...play it

_local_1103_10   ldx #4                                   ; test for notes,'w,h,q,i,s'
_local_1103_20   cmp notes,x                              
                 +lbeq set_note_length                    
                 dex                                      
                 bpl _local_1103_20                       

                 cmp #'R'                                 ; rest?
                 +lbeq play_rest                          
                 cmp #'.'                                 ; dotted note?
                 +lbeq play_dot                           

                 ldx #5                                   ; test for v,o,t,x,u,m commands
_local_1103_30   cmp mutabl,x                             
                 +lbeq play_command                       
                 dex                                      
                 bpl _local_1103_30                       ; test all 5 characters in table

                 cmp #'#'                                 ; sharp?
                 +lbeq play_sharp                         
                 cmp #'$'                                 ; flat?
                 +lbeq play_flat                          

; .page
; Must be a digit here for Octave, Voice, envelope (T), filter (X), or volume (U)

                 sec                                      
                 sbc #'0'                                 ; mask nybble
                 cmp #10                                  ; must be in range 0..9
                 +lbcs play_bad_value                     

                 asl flag                                 ; octave, voice, envelope, filter, or volume?
                 bcs set_voice                            
                 asl flag                                 ; octave, envelope, filter, or volume?
                 bcs set_octave                           
                 asl flag                                 ; envelope, filter, or volume?
                 bcs set_envelope                         
                 asl flag                                 ; filter or volume?
                 bcc set_volume                           

