midd2                                                     
midwrk           =midd2-1                                 

                 jsr chkopn                               ; check for '('
                 jsr ptrget                               ; get pointer to descriptor of string-var
                 sta forpnt                               ; store for later use
                 sty forpnt+1                             
                 jsr chkstr                               ; check if string

                 jsr combyt                               ; look for comma, followed by 1 byte starting address
                 dex                                      ; adjust starting addr
                 stx hulp                                 ; store    " "

                 cmp #')'                                 ; finished?
                 beq _local_1065_10                       ; branch if so (use default length)
                 jsr combyt                               ; ..else get length
                 !text $2c                                

_local_1065_10   ldx #$ff                                 ; default length
                 stx z_p_temp_1                           
                 jsr chkcls                               ; look for ')'
                 lda #equal_token                         ; look for '='
                 jsr synchr                               
                 jsr frmevl                               ; bring on the source!
                 jsr chkstr                               ; nothing funny

                 ldy #2                                   ; get string descriptors
_local_1065_20   lda #forpnt                              ; target
                 jsr lda_far_ram1                         ; lda (forpnt),y
                 sta str1,y                               
                 jsr indfmo                               ; source
                 sta str2,y                               
                 dey                                      
                 bpl _local_1065_20                       

; Test for target string in text was removed-  all strings are copied to
; string RAM when they are created.

                 sec                                      ; adjust pointer to source string so that the same
                 lda str2+1                               ; ..index can load & save
                 sbc hulp                                 
                 sta str2+1                               
                 bcs _local_1065_30                       
                 dec str2+2                               

_local_1065_30   lda z_p_temp_1                           ; get specified length (or default)
                 cmp str2                                 ; compare with length of source
                 bcc _local_1065_40                       ; ok if less,
                 lda str2                                 ; ..else use length of source
_local_1065_40   tax                                      
                 beq _local_1065_80                       ; done if length=0
                 clc                                      
                 adc hulp                                 ; add length to starting posn.
                 +lbcs fcerr                              ; illegal quantity error if > 256
                 cmp str1                                 
                 bcc _local_1065_60                       
                 +lbne fcerr                              ; ...or if > target length

_local_1065_60   ldy hulp                                 ; get adjusted starting address
_local_1065_70   phx                                      
                 ldx #str1+1                              
                 lda #str2+1                              
                 jsr lda_far_ram1                         ; fetch from string bank
                 jsr sta_far_ram1                         ; this is what it's all about
                 iny                                      
                 plx                                      
                 dex                                      
                 bne _local_1065_70                       ; keep going for specified length

_local_1065_80   +lbra frefac                             ; free up temp. string, rts

;.end

; .page
; .subttl  AUTO

; AUTO Increment
;   Syntax :    auto {line-number} (line-number = 0 means turn off)

